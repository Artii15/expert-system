package com.sample

import java.util.LinkedList;

rule "init"
	when
	then
		insert(new Attribute("rodzaj", "nieznany"));
end

rule "Wykryj rodzaj"
    when
        $rodzaj : Attribute( name == "rodzaj", value == "nieznany" )
    then
        Questionaire questionaire = new Questionaire("Wykryj rodzaj");
        LinkedList<String> answers = questionaire.askUser();
       	
       	retract($rodzaj);
       	for(String answer : answers) {
       		insert(new Attribute("rodzaj", answer));
       	}
end

rule "Określ rasę"
	when
		$rodzaj : Attribute( name == "rodzaj", value == "humanoid" )
	then
		Questionaire questionaire = new Questionaire("Określ rasę");
		LinkedList<String> answers = questionaire.askUser();
       	
       	for(String answer : answers) {
       		insert(new Attribute("rasa", answer));
       	}
end

rule "Określ płeć"
	when
		$humanoid : Attribute( name == "rodzaj", value == "humanoid" )
	then
		Questionaire questionaire = new Questionaire("Określ płeć");
       	LinkedList<String> answers = questionaire.askUser();
       	
       	for(String answer : answers) {
       		insert(new Attribute("płeć", answer));
       	}
end

rule "Określ typ potwora"
	when
		Attribute( name == "rodzaj", value == "potwór" )
	then
		Questionaire questionaire = new Questionaire("Określ typ potwora");
       	LinkedList<String> answers = questionaire.askUser();
       	
       	for(String answer : answers) {
       		insert(new Attribute("typ potwora", answer));
       	}
end

rule "Określ szczególną cechę potwora"
	when
		Attribute( name == "rodzaj", value == "potwór" )
	then
		Questionaire questionaire = new Questionaire("Określ szczególną cechę potwora");
       	LinkedList<String> answers = questionaire.askUser();
       	
       	for(String answer : answers) {
       		insert(new Attribute("cecha szczególna", answer));
       	}
end

rule "To wampir"
	when
		Attribute( name == "typ potwora", value == "inteligentny")
		Attribute( name == "cecha szczególna", value == "Pije krew")
	then
		Questionaire questionaire = new Questionaire("To wampir");
       	questionaire.informUser();
end

rule "To wilkołak"
	when
		Attribute( name == "typ potwora", value == "inteligentny")
		Attribute( name == "cecha szczególna", value == "Reaguje na pełnię księżyca")
	then
		Questionaire questionaire = new Questionaire("To wilkołak");
       	questionaire.informUser();
end

rule "To sukkub"
	when
		Attribute( name == "typ potwora", value == "inteligentny")
		Attribute( name == "cecha szczególna", value == "Wysysa siły witalne")
	then
		Questionaire questionaire = new Questionaire("To sukkub");
       	questionaire.informUser();
end

rule "Określ czy jest mutantem"
	when
		Attribute( name == "rasa", value == "człowiek" )
		Attribute( name == "płeć", value == "mężczyzna" )
		Attribute( name == "profesja", value == "walka" )
		not Attribute( name == "profesja", value == "sprawowanie władzy" )
		not Attribute( name == "profesja", value == "twórczość" )
		not Attribute( name == "doradza" )
	then
		Questionaire questionaire = new Questionaire("Określ czy jest mutantem");
       	LinkedList<String> answers = questionaire.askUser();
       	
       	for(String answer : answers) {
       		insert(new Attribute("mutant", answer));
       	}
end

rule "Określ cechę szczególną wiedźmina"
	when
		Attribute( name == "mutant", value == "Tak" )
	then
		Questionaire questionaire = new Questionaire("Określ cechę szczególną wiedźmina");
       	LinkedList<String> answers = questionaire.askUser();
       	
       	for(String answer : answers) {
       		insert(new Attribute("cecha szczególna", answer));
       	}
end

rule "To Coen"
	when
		Attribute( name == "mutant", value == "Tak" )
		Attribute( name == "cecha szczególna", value == "Żółte oczy" )
	then
		Questionaire questionaire = new Questionaire("To Coen");
       	questionaire.informUser();
end

rule "To Eskel"
	when
		Attribute( name == "mutant", value == "Tak" )
		Attribute( name == "cecha szczególna", value == "Duża blizna na twarzy" )
	then
		Questionaire questionaire = new Questionaire("To Eskel");
       	questionaire.informUser();
end

rule "To Geralt"
	when
		Attribute( name == "mutant", value == "Tak" )
		Attribute( name == "cecha szczególna", value == "Białe włosy" )
	then
		Questionaire questionaire = new Questionaire("To Geralt");
       	questionaire.informUser();
end

rule "Określ profesje"
	when
		Attribute( name == "rodzaj", value == "humanoid" )
	then
		Questionaire questionaire = new Questionaire("Określ profesje");
       	LinkedList<String> answers = questionaire.askUser();
       	
       	for(String answer : answers) {
       		insert(new Attribute("profesja", answer));
       	}
end

rule "Określ którymi państwami włada (mężczyzna)"
	when
		Attribute( name == "profesja", value == "sprawowanie władzy" )
		Attribute( name == "rasa", value == "człowiek" )
		Attribute( name == "płeć", value == "mężczyzna" )
		not Attribute( name == "profesja", value == "magia" )
	then
		Questionaire questionaire = new Questionaire("Określ którymi państwami włada (mężczyzna)");
       	LinkedList<String> answers = questionaire.askUser();
       	
       	for(String answer : answers) {
       		insert(new Attribute("zarządzane państwo", answer));
       	}
end

rule "Określ którymi państwami włada (kobieta)"
	when
		Attribute( name == "profesja", value == "sprawowanie władzy" )
		Attribute( name == "rasa", value != "krasnolud" )
		Attribute( name == "płeć", value == "kobieta" )

	then
		Questionaire questionaire = new Questionaire("Określ którymi państwami włada (kobieta)");
       	LinkedList<String> answers = questionaire.askUser();
       	
       	for(String answer : answers) {
       		insert(new Attribute("zarządzane państwo", answer));
       	}
end

rule "To Meve"
	when
		Attribute( name == "rasa", value == "człowiek" )
		not Attribute( name == "profesja", value == "magia" )
		not Attribute( name == "profesja", value == "twórczość" )
		(or
			not Attribute( name == "romans" )
			Attribute( name == "romans", value == "nie" )
		)
		(or
			not Attribute( name == "kolor włosów" )
			Attribute( name == "kolor włosów", value == "białe" )
		)
	then
		Questionaire questionaire = new Questionaire("To Meve");
       	questionaire.informUser();
end

rule "To Calanthe"
	when
		Attribute( name == "zarządzane państwo", value == "Cintra" )
		Attribute( name == "rasa", value == "człowiek" )
		not Attribute( name == "profesja", value == "magia" )
		(or
			Attribute( name == "kolor włosów", value == "popielate" )
			Attribute( name == "kolor włosów" )
		)
		(or
			not Attribute( name == "romans" )
			Attribute( name == "romans", value == "nie" )
		)
	then
		Questionaire questionaire = new Questionaire("To Calanthe");
       	questionaire.informUser();
end

rule "To Francesca Findabair"
	when
		(or
			Attribute( name == "zarządzane państwo", value == "Dol Blathanna" )
			not Attribute( name == "zarządzane państwo" )
		)
		Attribute( name == "rasa", value == "elf" )
		Attribute( name == "płeć", value == "kobieta" )
		Attribute( name == "profesja", value == "magia" )
		Attribute( name == "profesja", value == "sprawowanie władzy" )
	then
		Questionaire questionaire = new Questionaire("To Francesca Findabair");
       	questionaire.informUser();
end

rule "To Esterad Thyssen"
	when
		Attribute( name == "zarządzane państwo", value == "Kovir" )
	then
		Questionaire questionaire = new Questionaire("To Esterad Thyssen");
       	questionaire.informUser();
end

rule "To Emhyr"
	when
		Attribute( name == "zarządzane państwo", value == "Nilfgaard" )
	then
		Questionaire questionaire = new Questionaire("To Emhyr");
       	questionaire.informUser();
end

rule "To Henselt"
	when
		Attribute( name == "zarządzane państwo", value == "Kaedwen" )
	then
		Questionaire questionaire = new Questionaire("To Henselt");
       	questionaire.informUser();
end

rule "To Demawend"
	when
		Attribute( name == "zarządzane państwo", value == "Aedirn" )
	then
		Questionaire questionaire = new Questionaire("To Demawend");
       	questionaire.informUser();
end

rule "To Foltest"
	when
		Attribute( name == "zarządzane państwo", value == "Temeria" )
	then
		Questionaire questionaire = new Questionaire("To Foltest");
       	questionaire.informUser();
end

rule "To Radovid"
	when
		Attribute( name == "zarządzane państwo", value == "Redania" )
		Attribute( name == "płeć", value == "mężczyzna" )
	then
		Questionaire questionaire = new Questionaire("To Radovid");
       	questionaire.informUser();
end

rule "To bard Jaskier"
	when
		Attribute( name == "profesja", value == "twórczość" )
		Attribute( name == "płeć", value == "mężczyzna" )
		Attribute( name == "rasa", value == "człowiek" )
		not Attribute( name == "profesja", value == "walka" )
		not Attribute( name == "profesja", value == "magia" )
		not Attribute( name == "profesja", value == "sprawowanie władzy" )
	then
		Questionaire questionaire = new Questionaire("To bard Jaskier");
       	questionaire.informUser();
end

rule "Określ czy to zaręczony krasnolud"
	when
		Attribute( name == "rasa", value == "krasnolud" )
	then
		Questionaire questionaire = new Questionaire("Określ czy to zaręczony krasnolud");
       	LinkedList<String> answers = questionaire.askUser();
       	
       	for(String answer : answers) {
       		insert(new Attribute("zaręczyny", answer));
       	}
end

rule "To Zoltan Chivay"
	when
		Attribute( name == "profesja", value == "walka" )
		Attribute( name == "płeć", value == "mężczyzna" )
		Attribute( name == "rasa", value == "krasnolud" )
		Attribute( name == "zaręczyny", value == "Tak" )
		not Attribute( name == "profesja", value == "magia" )
		not Attribute( name == "profesja", value == "sprawowanie władzy" )
		not Attribute( name == "profesja", value == "twórczość" )
	then
		Questionaire questionaire = new Questionaire("To Zoltan Chivay");
       	questionaire.informUser();
end

rule "To Yarpen"
	when
		Attribute( name == "profesja", value == "walka" )
		Attribute( name == "płeć", value == "mężczyzna" )
		Attribute( name == "rasa", value == "krasnolud" )
		Attribute( name == "zaręczyny", value == "Nie" )
		not Attribute( name == "profesja", value == "magia" )
		not Attribute( name == "profesja", value == "sprawowanie władzy" )
		not Attribute( name == "profesja", value == "twórczość" )
	then
		Questionaire questionaire = new Questionaire("To Yarpen");
       	questionaire.informUser();
end

rule "Określ czy romansowała z Geraltem"
	when 
		Attribute( name == "płeć", value == "kobieta" )
		Attribute( name == "rasa", value == "człowiek" )
	then
		Questionaire questionaire = new Questionaire("Określ czy romansowała z Geraltem");
        LinkedList<String> answers = questionaire.askUser();
       	
       	for(String answer : answers) {											 
       		insert(new Attribute("romans", answer));
       	}
end

rule "Określ czy doradza jakiemuś królowi"
	when 
		Attribute( name == "rasa", value == "człowiek" )
		Attribute( name == "profesja", value == "magia" )
	then
		Questionaire questionaire = new Questionaire("Określ czy doradza jakiemuś królowi");
        LinkedList<String> answers = questionaire.askUser();
       	
       	for(String answer : answers) {											 
       		insert(new Attribute("doradza", answer));
       	}
end

rule "To Filippa Eilhart"
	when  
		Attribute( name == "płeć", value == "kobieta" )
		not Attribute( name == "profesja", value == "twórczość" )
		(or 
			not Attribute( name == "romans" )
			Attribute( name == "romans", value == "nie" )
		)
		(or 
			not Attribute( name == "kolor włosów" )
			Attribute( name == "kolor włosów", value == "czarne" )
		)
		(or
			(and 
				Attribute( name == "zarządzane państwo", value == "Redania" )
				not Attribute( name == "doradza" )
			)
			(and
				not Attribute( name == "zarządzane państwo" )
				Attribute( name == "doradza", value == "Radovid" )
			)
		)
	then
		Questionaire questionaire = new Questionaire("To Filippa Eilhart");
       	questionaire.informUser();
end

rule "To Sabrina Glevissig"
	when 
		Attribute( name == "doradza", value == "Henselt" ) 
		Attribute( name == "płeć", value == "kobieta" )
		not Attribute( name == "profesja", value == "twórczość" )
		not Attribute( name == "profesja", value == "sprawowanie władzy" )
		(or 
			Attribute( name == "kolor włosów", value == "brązowe" )
			not Attribute( name == "kolor włosów" )
		)
		(or 
			not Attribute( name == "romans" )
			Attribute( name == "romans", value == "nie" )
		)
	then
		Questionaire questionaire = new Questionaire("To Sabrina Glevissig");
       	questionaire.informUser();
end

rule "To Triss1"
	when  
		Attribute( name == "kolor włosów", value == "rude" )
		(or
			not Attribute( name == "doradza" )
			Attribute( name == "doradza", value == "Foltest" )
		)
		(or
			not Attribute( name == "romans" )
			Attribute( name == "romans", value == "tak" )
		)
		Attribute( name == "profesja", value == "magia" )
		not Attribute( name == "profesja", value == "twórczość" )
		not Attribute( name == "profesja", value == "sprawowanie władzy" )
	then
		Questionaire questionaire = new Questionaire("To Triss");
       	questionaire.informUser();
end

rule "To Triss2"
	when  
		Attribute( name == "doradza", value == "Foltest" )
		(or
			Attribute( name == "kolor włosów", value == "rude" )
			not Attribute( name == "kolor włosów" )
		)
		(or
			Attribute( name == "romans", value == "tak" )
			not Attribute( name == "romans" )
		)
		Attribute( name == "płeć", value == "kobieta" )	
		not Attribute( name == "profesja", value == "twórczość" )
		not Attribute( name == "profesja", value == "sprawowanie władzy" )
	then
		Questionaire questionaire = new Questionaire("To Triss");
       	questionaire.informUser();
end

rule "To Triss3"
	when  
		Attribute( name == "romans", value == "tak" )		
		(or
			Attribute( name == "kolor włosów", value == "rude" )
			Attribute( name == "doradza", value == "Foltest" )
		)
		
		not Attribute( name == "profesja", value == "twórczość" )
		not Attribute( name == "profesja", value == "sprawowanie władzy" )
	then
		Questionaire questionaire = new Questionaire("To Triss");
       	questionaire.informUser();
end

rule "To Yennefer"
	when 
		Attribute( name == "płeć", value == "kobieta" )
		Attribute( name == "profesja", value == "magia" )
		not Attribute( name == "doradza" ) 
		Attribute( name == "romans", value == "tak" )
		
		Attribute( name == "kolor włosów", value == "czarne" )
	then
		Questionaire questionaire = new Questionaire("To Yennefer");
       	questionaire.informUser();
end

rule "Określ kolor włosów kobiety"
	when 
		Attribute( name == "rasa", value == "człowiek" )
		Attribute( name == "płeć", value == "kobieta" )
	then
		Questionaire questionaire = new Questionaire("Określ kolor włosów kobiety");
        LinkedList<String> answers = questionaire.askUser();
       	
       	for(String answer : answers) {											 
       		insert(new Attribute("kolor włosów", answer));
       	}
end

rule "To Ciri"
	when 
		not Attribute( name == "profesja", value == "sprawowanie władzy" )
		not Attribute( name == "profesja", value == "twórczość" )
		Attribute( name == "płeć", value == "kobieta" )
		Attribute( name == "kolor włosów", value == "popielate" )
		(or
			Attribute( name == "romans", value == "nie" )
			not Attribute( name == "romans" )
		)
		(or
			Attribute( name == "profesja", value == "magia" )
			Attribute( name == "profesja", value == "walka" )
		)
	then
		Questionaire questionaire = new Questionaire("To Ciri");
       	questionaire.informUser();
end

rule "Nie znaleziono rozwiązania"
		salience( -1 )
	when
		exists Attribute()
	then
		System.out.println("Nie wiem o kogo chodzi");
end